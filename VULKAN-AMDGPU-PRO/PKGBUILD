# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <https://github.com/blacksky3>
#Credits: Janusz Lewandowski <lew21@xtreeme.org>
#Credits: David McFarland <corngood@gmail.com>
#Credits: Andrew Shark <ashark @at@ linuxcomp.ru>


#Upstream pages
#https://aur.archlinux.org/pkgbase/amdgpu-pro-installer
#https://github.com/Ashark/archlinux-amdgpu-pro
#https://repo.radeon.com/amdgpu

major=23.10
major_short=23.10
minor=1595145
ubuntu_ver=22.04
repo_folder_ver=5.5.1
amf_version=1.4.30
liba_version=1.0

pkgbase=vulkan-amdgpu-pro
pkgname=(amf-amdgpu-pro vulkan-amdgpu-pro lib32-vulkan-amdgpu-pro)
pkgver=${major}_${minor}
pkgrel=1
arch=(x86_64)
url='https://repo.radeon.com/amdgpu'
license=(custom: multiple)
source=(https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/a/amf-amdgpu-pro/amf-amdgpu-pro_${amf_version}-${minor}.${ubuntu_ver}_amd64.deb
        https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/liba/libamdenc-amdgpu-pro/libamdenc-amdgpu-pro_${liba_version}-${minor}.${ubuntu_ver}_amd64.deb
        https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/v/vulkan-amdgpu-pro/vulkan-amdgpu-pro_${major_short}-${minor}.${ubuntu_ver}_i386.deb
        https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/v/vulkan-amdgpu-pro/vulkan-amdgpu-pro_${major_short}-${minor}.${ubuntu_ver}_amd64.deb)

extract_deb(){
  local tmpdir="$(basename "${1%.deb}")"
  rm -Rf "$tmpdir"
  mkdir "$tmpdir"
  cd "$tmpdir"
  ar x "$1"
  tar -C "${pkgdir}" -xf data.tar.xz
}

move_libdir(){
  local deb_libdir="$1"
  local arch_libdir="$2"

  if [ -d "${pkgdir}/${deb_libdir}" ]; then
    if [ ! -d "${pkgdir}/${arch_libdir}" ]; then
      mkdir -p "${pkgdir}/${arch_libdir}"
    fi
    mv -t "${pkgdir}/${arch_libdir}/" "${pkgdir}/${deb_libdir}"/*
    find ${pkgdir} -type d -empty -delete
  fi
}

move_copyright(){
  find ${pkgdir}/usr/share/doc -name "changelog.Debian.gz" -delete
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  find ${pkgdir}/usr/share/doc -name "copyright" -exec mv {} ${pkgdir}/usr/share/licenses/${pkgname} \;
  find ${pkgdir}/usr/share/doc -type d -empty -delete
}

package_amf-amdgpu-pro(){
  pkgdesc='AMDGPU Pro Advanced Multimedia Framework'
  license=(custom: AMDGPU-PRO EULA)
  depends=(libdrm vulkan-amdgpu-pro=${major}_${minor}-${pkgrel})
  optdepends=('rocm-opencl-runtime: Warning unspecified optdep description')
  conflicts=(amf-amdgpu-pro-21.20)

  extract_deb "${srcdir}"/amf-amdgpu-pro_${amf_version}-${minor}.${ubuntu_ver}_amd64.deb
  extract_deb "${srcdir}"/libamdenc-amdgpu-pro_${liba_version}-${minor}.${ubuntu_ver}_amd64.deb
  move_libdir "opt/amdgpu-pro/lib/x86_64-linux-gnu" "usr/lib"
  move_copyright
}

package_vulkan-amdgpu-pro(){
  pkgdesc='AMDGPU Pro Vulkan driver'
  license=(custom: AMDGPU-PRO EULA)
  provides=(vulkan-driver)
  depends=(vulkan-icd-loader)
  optdepends=('openssl: Warning unspecified optdep description')
  conflicts=(vulkan-amdgpu-pro-21.20)

  extract_deb "${srcdir}"/vulkan-amdgpu-pro_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
  move_libdir "opt/amdgpu-pro/lib/x86_64-linux-gnu" "usr/lib"
  move_copyright

  # extra_commands:
  mkdir -p "${pkgdir}"/usr/share/vulkan/icd.d/
  mv "${pkgdir}"/opt/amdgpu-pro/etc/vulkan/icd.d/amd_icd64.json "${pkgdir}"/usr/share/vulkan/icd.d/amd_pro_icd64.json
  mv "${pkgdir}"/usr/lib/amdvlk64.so "${pkgdir}"/usr/lib/amdvlkpro64.so
  sed -i "s#/opt/amdgpu-pro/lib/x86_64-linux-gnu/amdvlk64.so#/usr/lib/amdvlkpro64.so#" "${pkgdir}"/usr/share/vulkan/icd.d/amd_pro_icd64.json
  find ${pkgdir} -type d -empty -delete
}

package_lib32-vulkan-amdgpu-pro(){
  pkgdesc='AMDGPU Pro Vulkan driver (32-bit)'
  license=(custom: AMDGPU-PRO EULA)
  provides=(lib32-vulkan-driver)
  depends=(lib32-vulkan-icd-loader vulkan-amdgpu-pro=${major}_${minor}-${pkgrel})
  optdepends=('lib32-openssl: Warning unspecified optdep description')
  conflicts=(lib32-vulkan-amdgpu-pro-21.20)

  extract_deb "${srcdir}"/vulkan-amdgpu-pro_${major_short}-${minor}.${ubuntu_ver}_i386.deb
  move_libdir "opt/amdgpu-pro/lib/i386-linux-gnu" "usr/lib32"
  move_copyright

  # extra_commands:
  mkdir -p "${pkgdir}"/usr/share/vulkan/icd.d/
  mv "${pkgdir}"/opt/amdgpu-pro/etc/vulkan/icd.d/amd_icd32.json "${pkgdir}"/usr/share/vulkan/icd.d/amd_pro_icd32.json
  mv "${pkgdir}"/usr/lib32/amdvlk32.so "${pkgdir}"/usr/lib32/amdvlkpro32.so
  sed -i "s#/opt/amdgpu-pro/lib/i386-linux-gnu/amdvlk32.so#/usr/lib32/amdvlkpro32.so#" "${pkgdir}"/usr/share/vulkan/icd.d/amd_pro_icd32.json
  find ${pkgdir} -type d -empty -delete
}

sha256sums=('f1825098d8ea4efa3999094761ae5e26005eebfc3481011f4723f1cfb514b89f'
            'c0101de4551d50e1874a25f3130843dc2d01fd5e007b4be4db4468c66a66f605'
            '7e3dab457a37bade8306038c8f126ddd3438d31e5eb44bc5566ab41e7291870f'
            '3af060309f6da74b901a6adc4acc622bc73d36b6086fc77f7d69872a6229ac52')
